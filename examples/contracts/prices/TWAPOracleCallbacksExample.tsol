pragma ever-solidity >= 0.61.2;

pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "locklift/src/console.tsol";

import "../../../contracts/libraries/MsgFlag.tsol";
import "../../../contracts/libraries/UtilityGas.tsol";

import "../../../contracts/reservation/abstract/Reservable.tsol";
import "../../../contracts/reservation/abstract/TargetBalance.tsol";

import "../../../contracts/prices/oracle/interfaces/IOnObservationCallback.tsol";
import "../../../contracts/prices/oracle/interfaces/IOnRateCallback.tsol";
import "../../../contracts/prices/oracle/interfaces/ITWAPOracle.tsol";

contract TWAPOracleCallbacksExample is
    IOnRateCallback,
    IOnObservationCallback,
    Reservable,
    TargetBalance
{
    uint32 private static _nonce;
    ITWAPOracle private static _oracle;

    function _getTargetBalanceInternal()
        internal
        view
        override
        returns (uint128)
    {
        return UtilityGas.INITIAL_BALANCE;
    }

    constructor(optional(address) _remainingGasTo)
        public
        reserveAcceptAndRefund(_getTargetBalanceInternal(), _remainingGasTo, msg.sender)
    {}

    function onRateCallback(
        optional(Rate) _rate,
        uint128[],
        address _callbackRequester,
        TvmCell _payload
    )
        external
        override
        reserveAndRefund(
            _getTargetBalanceInternal(),
            abi.decode(_payload, address),
            abi.decode(_payload, address)
        )
    {
        Rate rate = _rate.get();

        console.log(format("Price 0/1: {}", rate.price0To1));
        console.log(format("Price 1/0: {}", rate.price1To0));
        console.log(format("From timestamp: {}", rate.fromTimestamp));
        console.log(format("To timestamp: {}", rate.toTimestamp));
        console.log(format("Requester: {}", _callbackRequester));
    }

    function onObservationCallback(
        optional(Observation) _observation,
        address _callbackRequester,
        TvmCell _payload
    )
        external
        override
        reserveAndRefund(
            _getTargetBalanceInternal(),
            abi.decode(_payload, address),
            abi.decode(_payload, address)
        )
    {
        Observation observation = _observation.get();

        console.log(format("Cumulative 0/1: {}", observation.price0To1Cumulative));
        console.log(format("Cumulative 1/0: {}", observation.price1To0Cumulative));
        console.log(format("Timestamp: {}", observation.timestamp));
        console.log(format("Requester: {}", _callbackRequester));
    }

    function rate(
        uint32 _fromTimestamp,
        uint32 _toTimestamp,
        optional(address) _remainingGasTo
    )
        external
        view
        reserve(_getTargetBalanceInternal())
    {
        address remainingGasTo = _remainingGasTo.hasValue() ? _remainingGasTo.get() : msg.sender;

        _oracle
            .rate{
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            }(
                _fromTimestamp,
                _toTimestamp,
                address(this),
                abi.encode(remainingGasTo)
            );
    }

    function observation(
        uint32 _timestamp,
        optional(address) _remainingGasTo
    )
        external
        view
        reserve(_getTargetBalanceInternal())
    {
        address remainingGasTo = _remainingGasTo.hasValue() ? _remainingGasTo.get() : msg.sender;

        _oracle
            .observation{
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            }(
                _timestamp,
                address(this),
                abi.encode(remainingGasTo)
            );
    }
}
